{-# OPTIONS --without-K --prop #-}
module pi where

open import prelude

import Relation.Unary as RU

open import Relation.Binary.Core
open import Relation.Binary.Structures
import Relation.Binary.Bundles as RBB using (Poset)

open import Poset

-- Negative Î 

open Distr
open IndexedPoset


Î â‚€ : {l l' : Level} â†’ (A : Set l) â†’ (B : A â†’ Set l') â†’ Set (l âŠ” l')
Î â‚€ A B = âˆ€ a â†’ B a

module RelÎ â‚€ {A A' : Set} {B : A â†’ Set} {B' : A' â†’ Set}
            (AÎµ : A â†’ A' â†’ Set)
            (BÎµ : âˆ€ {a a'} (aÎµ : AÎµ a a') â†’ B a â†’ B' a' â†’ Set) where

  Î Îµâ‚€ : Î â‚€ A B â†’ Î â‚€ A' B' â†’ Set
  Î Îµâ‚€ f f' = âˆ€ {a a'} (aÎµ : AÎµ a a') â†’ BÎµ aÎµ (f a) (f' a')

  Î Îµâ‚€-hprop : (âˆ€ {a a'} (aÎµ : AÎµ a a') â†’ Irrelevant (BÎµ aÎµ)) â†’ Irrelevant Î Îµâ‚€
  Î Îµâ‚€-hprop BIrr f f' = funext-impl Î» {a} â†’ funext-impl (Î» {a'} â†’ funext (Î» aÎµ â†’ BIrr aÎµ (f aÎµ) (f' aÎµ)))


Î  : (A : â„™oset) (B : IndexedPoset A) â†’ Set
Î  (A , PA) B =
  let B' a = (B at a) .fst in
  let instance
        PA : Poset A
        PA = PA
        PB : âˆ€ {a} â†’ Poset (B' a)
        PB {a} = (B at a) .snd
  in Î£[ f âˆˆ (âˆ€ (a : A) â†’ B' a) ] âˆ€ {a a'} (aa' : a âŠ‘ a') â†’ distr (B onRel aa') (f a) (f a')


Î -ext : (A : â„™oset) (B : IndexedPoset A) (f g : Î  A B) (Heq : (a : A .fst) â†’ f .fst a â‰¡ g .fst a) â†’ f â‰¡ g
Î -ext A B (f , monf) (g , mong) Heq rewrite (funext Heq) =
  PE.cong (_,_ g) (funext-impl (funext-impl (funext (Î» aa' â†’ distr-hProp {{(B at _) .snd}} {{(B at _) .snd}} (B onRel aa') (monf aa') (mong aa')))))

infix 8 _âŸ¶_

_âŸ¶_ : (A B : â„™oset) â†’ Set
A âŸ¶ B = Î  A (trivialIndexedPoset A B)

âŸ¶-ext : {A B : â„™oset} (f g : A âŸ¶ B) (Heq : (a : A .fst) â†’ f .fst a â‰¡ g .fst a) â†’ f â‰¡ g
âŸ¶-ext {A} {B} = Î -ext A (trivialIndexedPoset A B)


Î Îµ : {A A' : â„™oset} (B : IndexedPoset A) (B' : IndexedPoset A')
     (AÎµ : PREL A A') (BÎµ : IndexedPREL B B' AÎµ) â†’
     REL (Î  A B) (Î  A' B') lzero
Î Îµ _ _ AÎµ BÎµ f g = RelÎ â‚€.Î Îµâ‚€ AÎµ BÎµ (f .fst) (g .fst)

_âŸ¶Îµ_ : {A A' B B' : â„™oset} (AÎµ : ğ”»istr A A') (BÎµ : ğ”»istr B B') â†’ REL (A âŸ¶ B) (A' âŸ¶ B') lzero
_âŸ¶Îµ_ {A} {A'} {B} {B'} AÎµ BÎµ = Î Îµ (trivialIndexedPoset A B) (trivialIndexedPoset A' B') (ğ••istr AÎµ) Î» aa' â†’ ğ••istr BÎµ


Î Îµ-ext : {A A' : â„™oset} (B : IndexedPoset A) (B' : IndexedPoset A')
        (AÎµ AÎµ' : PREL A A') (BÎµ : IndexedPREL B B' AÎµ) (BÎµ' : IndexedPREL B B' AÎµ')
        (HA : AÎµ â‰¡ AÎµ') (HB : âˆ€ {a} {a'} (aÎµ : AÎµ' a a') â†’ BÎµ (PE.subst (Î» R â†’ R a a') (PE.sym HA) aÎµ) â‰¡ BÎµ' aÎµ) â†’
        Î Îµ B B' AÎµ BÎµ â‰¡ Î Îµ B B' AÎµ' BÎµ'
Î Îµ-ext B B' AÎµ AÎµ' BÎµ BÎµ' erefl HB = PE.cong (Î Îµ B B' AÎµ) {BÎµ} {BÎµ'} (funext-impl (funext-impl (funext HB)))

Î Îµ-hprop : {A A' : â„™oset} (B : IndexedPoset A) (B' : IndexedPoset A')
           (AÎµ : PREL A A') (BÎµ : IndexedPREL B B' AÎµ) â†’
           (âˆ€ {a a'} (aÎµ : AÎµ a a') â†’ Irrelevant (BÎµ aÎµ)) â†’
           Irrelevant (Î Îµ B B' AÎµ BÎµ)
Î Îµ-hprop _ _ AÎµ BÎµ BIrr = RelÎ â‚€.Î Îµâ‚€-hprop AÎµ BÎµ BIrr


reflÎ Îµ : {A : â„™oset} (B : IndexedPoset A) â†’
         Reflexive (Î Îµ B B (_âŠ‘_ {{A .snd}}) (id-toIndexedPREL B))
reflÎ Îµ _ {f} = f .snd

id-transÎ Îµ : {A : â„™oset} (B : IndexedPoset A) â†’
          Transitive (Î Îµ B B (_âŠ‘_ {{A .snd}}) (id-toIndexedPREL B))
id-transÎ Îµ {A} B {f} {g} {h} fg gh {a} {a'} aa'  = act-right â¦ƒ (B at a) .snd â¦„ â¦ƒ (B at a') .snd â¦„
                                     (B onRel aa') (fg aa') gha'
        where
          instance
            PBa' : Poset ((B at a') .fst)
            PBa' = (B at a') .snd
          gha' : g .fst a' âŠ‘ h .fst a'
          gha' = PE.subst (Î» d â†’ distr d (g .fst a') (h .fst a')) (indexedPoset-refl B a') (gh (refl {{A .snd}} {a'}))

antisymÎ Îµ : (A : â„™oset) (B : IndexedPoset A) â†’ Antisymmetric _â‰¡_ (Î Îµ B B (ğ••istr (id-ğ”»istr A)) (id-toIndexedPREL B))
antisymÎ Îµ A B {f} {g} fg gf = Î -ext A B f g ext
  where
    helper = Î» a x y â†’ PE.subst (Î» R â†’ ğ••istr R x y) (indexedPoset-refl B a)
    ext : (a : A .fst) â†’ f .fst a â‰¡ g .fst a
    ext a = let ra = refl {{A .snd}} in antisym {{(B at a) .snd}} (helper a (f .fst a) (g .fst a) (fg ra)) (helper a (g .fst a) (f .fst a) (gf ra))

Î áµ– : (A : â„™oset) (B : IndexedPoset A) â†’ Posetâ‚€ (Î  A B)
Î áµ– A B = record
  { _âŠ‘_ = Î Îµ B B AÎµ BÎµ
  ; isPoset = record
    { isPreorder = record
      { isEquivalence = PE.isEquivalence
      ; reflexive = Reflexive-respects-â‰¡ (Î Îµ B B AÎµ BÎµ) (Î» {f} â†’ reflÎ Îµ B {f})
      ; trans = Î» {f} {g} {h} â†’ id-transÎ Îµ B {f} {g} {h} }
    ; antisym = antisymÎ Îµ A B }
  ; âŠ‘-hProp = Î» {f} {g} â†’
    Î Îµ-hprop B B AÎµ BÎµ
             (Î» aÎµ a b â†’ distr-hProp {{(B at _) .snd}} {{(B at _) .snd}} (id-IndexedDistr B IndexedDistr.over aÎµ) a b)
             {f} {g}
  ; carrier-hSet =
      Î£-hset (âˆ€-hSet (Î» a â†’ carrier-hSet {{(B at a) .snd}}))
            Î» f â†’ hProp-to-hSet
              Î» pâ‚ pâ‚‚ â†’ funext-impl (funext-impl (funext (Î» aÎµ â†’
                  distr-hProp {{(B at _) .snd}}
                              {{(B at _) .snd}}
                              (B onRel aÎµ) _ _)))}
  where
    AÎµ = ğ••istr (id-ğ”»istr A)
    BÎµ = id-toIndexedPREL B

infix 8 _âŸ¶áµ–_

_âŸ¶áµ–_ : (A B : â„™oset) â†’ Posetâ‚€ (A âŸ¶ B)
A âŸ¶áµ– B = Î áµ– A (trivialIndexedPoset A B)


module Limits (A : â„™oset) (B : IndexedPoset A) where
  private
    instance
      PÎ  : Poset (Î  A B)
      PÎ  = Î áµ– A B
      PA : Poset (A .fst)
      PA = A .snd
      PB : âˆ€ {a} â†’ Poset ((B at a) .fst)
      PB {a} = (B at a) .snd


  âœ Î  : (âœ B : (a : A .fst) â†’ Initial ((B at a) .fst)) â†’ Initial (Î  A B)
  âœ Î  âœ B = record
    { bot = âœ Ï€
    ; is-bot = isâœ Ï€
    ; is-bot-spec = Î» {f} h â†’ Î -ext A B f âœ Ï€ (Î» a â†’ is-bot-spec (âœ B a) {f .fst a} (h a))
    ; bot-is-bot = Î» a â†’ bot-is-bot (âœ B a)
    ; bot-min = Î» hbot aÎµ â†’ down-repr-to (B onRel aÎµ) (bot-min (âœ B _) (hbot _))
    ; bot-smallest = Î» {b} {b'} bb' hbot a â†’
      bot-smallest (âœ B a)
         (PE.subst (Î» d â†’ ğ••istr d (b .fst a) (b' .fst a)) (indexedPoset-refl B a) (bb' refl))
         (hbot a)
    }
    where
    âœ Ï€ : Î  A B
    âœ Ï€ = (Î» a â†’ bot (âœ B a)), Î» {a} aÎµ â†’ bot-min-over (âœ B a) (B onRel aÎµ)
    isâœ Ï€ : Î  A B â†’ Prop
    isâœ Ï€ f = (a : A .fst) â†’ is-bot (âœ B a) (f .fst a)

  ?Î  : ((a : A .fst) â†’ Final ((B at a) .fst)) â†’ Final (Î  A B)
  ?Î  ?B = record
    { top = top âˆ˜ ?B , Î» {_} {a} aÎµ â†’ top-max-over (?B a) (B onRel aÎµ)
    ; is-top = Î» f â†’ (a : A .fst) â†’ is-top (?B a) (f .fst a)
    ; is-top-spec = Î» {f} h â†’ Î -ext A B f _ (Î» a â†’ is-top-spec (?B a) {f .fst a} (h a))
    ; top-is-top = Î» a â†’ top-is-top (?B a)
    ; top-max = Î» htop aÎµ â†’ up-repr-to (B onRel aÎµ) (top-max (?B _) (htop _))
    ; top-greatest = Î» {f} {g} fg htop a â†’ top-greatest (?B a)
                   (PE.subst (Î» d â†’ ğ••istr d (f .fst a) (g .fst a)) (indexedPoset-refl B a) (fg refl)) (htop a)
    }


âœ âŸ¶ : (A B : â„™oset) (âœ B : ğ•€nitial B) â†’ Initial (A âŸ¶ B) {{A âŸ¶áµ– B}}
âœ âŸ¶ A B âœ B = Limits.âœ Î  A (trivialIndexedPoset A B) (Î» _ â†’ âœ B)


Î ' : (A : â„™oset) (B : IndexedPoset A) â†’ â„™oset
Î ' A B = Î  A B , Î áµ– A B


transÎ Îµ : {Ax Ay Az : â„™oset} (Bx : IndexedPoset Ax) (By : IndexedPoset Ay) (Bz : IndexedPoset Az)
          (Axy : ğ”»istr Ax Ay) (Ayz : ğ”»istr Ay Az) (Bxy : IndexedDistr Axy Bx By) (Byz : IndexedDistr Ayz By Bz) â†’
          âˆ€ {f g h} â†’ Î Îµ Bx By (ğ••istr Axy) (toIndexedPREL Axy Bx By Bxy) f g â†’ Î Îµ By Bz (ğ••istr Ayz) (toIndexedPREL Ayz By Bz Byz) g h â†’
          Î Îµ Bx Bz (ğ••istr (âˆ˜-ğ”»istr Axy Ayz)) (toIndexedPREL _ _ _ (âˆ˜-IndexedDistr Bx By Bz Axy Ayz Bxy Byz)) f h
transÎ Îµ Bx By Bz Axy Ayz Bxy Byz {f} {g} {h} fg gh {ax} {az} auxz =
  let axux = (âŠ‘-up Axy ax)
      fgax = fg axux
      ghaxz = gh auxz
  in PE.subst (Î» axz â†’ toIndexedPREL (âˆ˜-ğ”»istr Axy Ayz) Bx Bz (âˆ˜-IndexedDistr Bx By Bz Axy Ayz Bxy Byz) axz (f .fst ax) (h .fst az))
              (trans-âˆ˜-ğ”»istr-up Axy Ayz auxz)
              (trans-âˆ˜-IndexedDistr Bx By Bz Axy Ayz Bxy Byz axux auxz fgax ghaxz)

âŸ¶-on-refl : {A B : â„™oset} (f : A âŸ¶ B) (a : A .fst) â†’
               f .snd (refl {{A .snd}})  â‰¡ refl {{B .snd}} {f .fst a}
âŸ¶-on-refl {A} {B , PB} f a = âŠ‘-hProp {{PB}} _ _

âŸ¶-on-trans : {A : Set} {{PA : Poset A}} {B : â„™oset} (f : (A , PA) âŸ¶ B) {a b c : A} (ab : a âŠ‘ b) (bc : b âŠ‘ c) â†’
               f .snd (trans ab bc)  â‰¡ trans {{B .snd}} (f .snd ab) (f .snd bc)
âŸ¶-on-trans {A} {B , PB} f ab bc = âŠ‘-hProp {{PB}} _ _


-- composition of a monotone function f : A â†’ B (seen as a functor) and a functor X : B â†’ Distr
pullback : {A B : â„™oset} (f : A âŸ¶ B) (X : IndexedPoset B) â†’ IndexedPoset A
pullback {A} {B} f X = record
  { _at_ = Î» a â†’ X at (f .fst a)
  ; _onRel_ = Î» aÎµ â†’ X onRel (f .snd aÎµ)
  ; indexedPoset-refl = aux-refl
  ; indexedPoset-trans = aux-trans
  }
  where
    instance
      PA = A .snd
      PB = B .snd
    aux-refl : (a : A .fst) â†’ (X onRel f .snd refl) â‰¡ id-ğ”»istr (X at f .fst a)
    aux-refl a rewrite âŸ¶-on-refl {A} {B} f a = indexedPoset-refl X _
    aux-trans : {a b c : A .fst} (ab : a âŠ‘ b) (bc : b âŠ‘ c) â†’
                (X onRel f .snd (trans ab bc)) â‰¡
                âˆ˜-ğ”»istr (X onRel f .snd ab) (X onRel f .snd bc)
    aux-trans ab bc rewrite âŸ¶-on-trans {A .fst} {B} f ab bc =
      indexedPoset-trans X _ _

â‡’-Poset-to-âŸ¶ : {A B : Set} {{PA : Poset A}} {{PB : Poset B}}
                   (f : A â‡’-Poset B) â†’ ((A , PA) âŸ¶ (B , PB))
â‡’-Poset-to-âŸ¶ f = fun f , monotone f
  where
    open _â‡’-Poset_

indexed-pullback : {A A' B B' : â„™oset} (AÎµ : ğ”»istr A A') (BÎµ : ğ”»istr B B')
                   (f : A âŸ¶ B) (f' : A' âŸ¶ B') (fÎµ : (AÎµ âŸ¶Îµ BÎµ) f f')
                   (X : IndexedPoset B) (X' : IndexedPoset B') (XÎµ : IndexedDistr BÎµ X X') â†’
                   IndexedDistr  AÎµ (pullback f X) (pullback f' X')
indexed-pullback {A} {A'} {B} {B'} AÎµ BÎµ f f' fÎµ X X' XÎµ = record
  { _over_ = Î» aÎµ â†’ XÎµ over (fÎµ aÎµ)
  ; indexedDistr-naturality = Î» aâ‚â‚‚ aâ‚‚Îµ aâ‚Îµ a'â‚â‚‚ â†’ indexedDistr-naturality XÎµ (f .snd aâ‚â‚‚) (fÎµ aâ‚‚Îµ) (fÎµ aâ‚Îµ) (f' .snd a'â‚â‚‚) }
  where
    open IndexedDistr

-- (f : Î  A B) (f' : Î  A' B') (AÎµ ğ”»istr A A') (X : IndexedPoset B) â†’ IndexedPoset A {a a'} (aÎµ : a âŠ‘ a') IndexedDistr (B a)


module Î -Distr {Adom Bdom : â„™oset}
               (Acod : IndexedPoset Adom) (Bcod : IndexedPoset Bdom)
               (ABdom : ğ”»istr Adom Bdom) (ABcod : IndexedDistr ABdom Acod Bcod)
               where

  Î distr = Î Îµ Acod Bcod (ğ••istr ABdom) (toIndexedPREL ABdom Acod Bcod ABcod)

  private
    instance
      PiA : Poset (Î  Adom Acod)
      PiA = (Î ' Adom Acod) .snd
      PiB : Poset (Î  Bdom Bcod)
      PiB = (Î ' Bdom Bcod) .snd
      PAdom : Poset (Adom .fst)
      PAdom = Adom .snd
      PBdom : Poset (Bdom .fst)
      PBdom = Bdom .snd
      PAcod : {a : Adom .fst} â†’ Poset ((Acod at a) .fst)
      PAcod {a} = (Acod at a) .snd
      PBcod : âˆ€ {a} â†’ Poset ((Bcod at a) .fst)
      PBcod {b} = (Bcod at b) .snd

  open PosetDistr (Î  Adom Acod) (Î  Bdom Bcod) Î distr
  open IndexedDistr

  Î PosetDistr : PosetDistr
  Î PosetDistr = record
    { act-left =  Î» {f} {g} {h} fg gh {a} {b} ab â†’
      let aÎµ = down-repr-from ABdom ab
          a'b = âŠ‘-down ABdom b
          hA = IndexedDistrHelper.act-left-compat ABdom Acod Bcod ABcod
      in sğ••istr hA (trans-âˆ˜-ğ”»istr (Acod onRel aÎµ) (ABcod over a'b) (fg aÎµ) (gh a'b))
      -- so long for loosy abstract arguments...
      -- transÎ Îµ Acod Acod Bcod (id-ğ”»istr Adom) ABdom
      --         (id-IndexedDistr Acod) ABcod
      --         {f} {g} {h}
      --         (Î» {a} {a'} aÎµ â†’ fg aÎµ) (Î» {a'} {b} a'b â†’ gh a'b)
    ; act-right = Î» {f} {g} {h} fg gh {a} {b} ab â†’
      let bÎµ = up-repr-from ABdom ab
          ab' = âŠ‘-up ABdom a
          hB = IndexedDistrHelper.act-right-compat ABdom Acod Bcod ABcod
      in sğ••istr hB (trans-âˆ˜-ğ”»istr (ABcod over ab') (Bcod onRel bÎµ)
                                    (fg ab') (gh bÎµ))
    ; distr-hProp = Î» {f} {g} â†’
      Î Îµ-hprop Acod Bcod (ğ••istr ABdom)
               (toIndexedPREL ABdom Acod Bcod ABcod)
               (Î» aÎµ a b â†’ distr-hProp (ABcod over aÎµ) a b)
               {f} {g} }

  open _â‡’-Poset_
  upâ‚€ : Î  Adom Acod â†’ Î  Bdom Bcod
  upâ‚€ f = (Î» b â†’ (ABcod over (âŠ‘-down ABdom b)) â†Ÿ (f .fst (ABdom â†¡ b))) ,
          Î» {b} {b'} bÎµ â†’ up-mon (ABcod over âŠ‘-down ABdom b)
                                  (Bcod onRel bÎµ)
                                  (Acod onRel (down ABdom .monotone bÎµ))
                                  (ABcod over âŠ‘-down ABdom b')
                                  (PE.sym (indexedDistr-naturality ABcod _ _ _ _))
                                  (f .snd (down ABdom .monotone bÎµ))

  upâ‚€-mon : _âŠ‘_ =[ upâ‚€ ]â‡’ _âŠ‘_
  upâ‚€-mon fg {b} {b'} bÎµ =
    up-mon (ABcod over âŠ‘-down ABdom b)
            (Bcod onRel bÎµ)
            (Acod onRel (down ABdom .monotone bÎµ))
            (ABcod over âŠ‘-down ABdom b')
            (PE.sym (indexedDistr-naturality ABcod _ _ _ _))
            (fg (down ABdom .monotone bÎµ))

  updef : (Î ' Adom Acod) â‡’-â„™oset (Î ' Bdom Bcod)
  updef = record { fun = upâ‚€ ; monotone = Î» {f} {g} â†’ upâ‚€-mon {f} {g} }

  downâ‚€ : Î  Bdom Bcod â†’ Î  Adom Acod
  downâ‚€ f =
    (Î» a â†’ (ABcod over âŠ‘-up ABdom a) â†¡ (f .fst (ABdom â†Ÿ a))),
    Î» {a} {a'} aÎµ â†’ down-mon (ABcod over âŠ‘-up ABdom a)
                              (Bcod onRel (up ABdom .monotone aÎµ))
                              (Acod onRel aÎµ)
                              (ABcod over âŠ‘-up ABdom a')
                              (PE.sym (indexedDistr-naturality ABcod _ _ _ _))
                              (f .snd (up ABdom .monotone aÎµ))

  downâ‚€-mon :  _âŠ‘_ =[ downâ‚€ ]â‡’ _âŠ‘_
  downâ‚€-mon fg {a} {a'} aÎµ =
    down-mon (ABcod over âŠ‘-up ABdom a)
              (Bcod onRel (up ABdom .monotone aÎµ))
              (Acod onRel aÎµ)
              (ABcod over âŠ‘-up ABdom a')
              (PE.sym (indexedDistr-naturality ABcod _ _ _ _))
              (fg (up ABdom .monotone aÎµ))

  downdef : (Î ' Bdom Bcod) â‡’-â„™oset (Î ' Adom Acod)
  downdef = record { fun = downâ‚€ ; monotone = Î» {f} {g} â†’ downâ‚€-mon {f} {g} }

  Î LeftRepresentable : LeftRepresentable
  Î LeftRepresentable = record
    { up = updef
    ; up-repr-from = Î» {f} {g} fg {b} bÎµ â†’
      let ab = act-right ABdom (âŠ‘-down ABdom b) bÎµ
          h = PE.sym (IndexedDistrHelper.act-right-compat ABdom Acod Bcod ABcod)
      in
      DistrHelper.up-left (ABcod over (âŠ‘-down ABdom b)) (Bcod onRel bÎµ)
                          (sğ••istr h (fg ab))
    ; up-repr-to = Î» {f} {g} fg {a} {b} ab â†’
      let bÎµ = up-repr-from ABdom ab
          aÎµ = down-up-greater ABdom a
          xÎµ = act-right ABdom (âŠ‘-down ABdom (ABdom â†Ÿ a)) bÎµ
          hB = IndexedDistrHelper.act-right-compat ABdom Acod Bcod ABcod
          hA = IndexedDistrHelper.act-left-compat ABdom Acod Bcod ABcod
          AB = ABcod over (âŠ‘-down ABdom (ABdom â†Ÿ a))
          part1 = f .snd aÎµ
          part2 = âŠ‘-up AB (f .fst (ABdom â†¡ (ABdom â†Ÿ a)))
          part3 = fg bÎµ
      in sğ••istr hA (trans-âˆ˜-ğ”»istr (Acod onRel aÎµ) (ABcod over xÎµ)
                                  part1
                                  (sğ••istr hB (trans-âˆ˜-ğ”»istr AB (Bcod onRel bÎµ)
                                                            part2 part3))) }

  Î RightRepresentable : RightRepresentable
  Î RightRepresentable = record
    { down = downdef
    ; down-repr-from = Î» {f} {g} fg {a} {a'} aÎµ â†’
      let ab = act-left ABdom aÎµ (âŠ‘-up ABdom a')
          h = PE.sym (IndexedDistrHelper.act-left-compat ABdom Acod Bcod ABcod)
      in DistrHelper.down-right (Acod onRel aÎµ) (ABcod over (âŠ‘-up ABdom a'))
                                  (sğ••istr h (fg ab))
    ; down-repr-to = Î» {f} {g} fg {a} {b} ab â†’
      let aÎµ = down-repr-from ABdom ab
          bÎµ = up-down-lower ABdom b
          xÎµ = act-left ABdom aÎµ (âŠ‘-up ABdom (ABdom â†¡ b))
          hB = IndexedDistrHelper.act-right-compat ABdom Acod Bcod ABcod
          hA = IndexedDistrHelper.act-left-compat ABdom Acod Bcod ABcod
          AB = ABcod over (âŠ‘-up ABdom (ABdom â†¡ b))
          part1 = fg aÎµ
          part2 = âŠ‘-down AB (g .fst (ABdom â†Ÿ (ABdom â†¡ b)))
          part3 = g .snd bÎµ
      in sğ••istr hB (trans-âˆ˜-ğ”»istr (ABcod over xÎµ) (Bcod onRel bÎµ)
                                  (sğ••istr hA (trans-âˆ˜-ğ”»istr (Acod onRel aÎµ) AB
                                                            part1 part2))
                                  part3)}

  -- this postulate hold up to troubles with dependent rewrite
  -- downdef (updef f) a â‰¡ â†¡â†Ÿ (f (â†¡â†Ÿ a)) by definition of updef/downdef
  --                     â‰¡ â†¡â†Ÿ (f a)      by section-retraction property of ABdom
  --                     â‰¡ f a          by section-retraction property of ABcod (this only holds up to the previous equation)
  postulate sect-retr-Î  : (f : (Î ' Adom Acod) .fst) (a : Adom .fst) â†’ (downdef .fun (updef .fun f)) .fst a â‰¡ f .fst a

  Î ğ”»istr : ğ”»istr (Î ' Adom Acod) (Î ' Bdom Bcod)
  Î ğ”»istr = record
    { distr = Î distr
    ; isDistr = record
      { isPosetDistr = Î PosetDistr
      ; isLeftRepresentable = Î LeftRepresentable
      ; isRightRepresentable = Î RightRepresentable
      }
    ; up-down-inverseË¡ =
      Î» f â†’ Î -ext Adom Acod _ _
        (Î» a â†’ sect-retr-Î  f a) }


open Î -Distr public using (Î ğ”»istr)
